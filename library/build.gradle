apply plugin: 'com.android.library'

// START BINTRAY RELEASE INFOS

ext {
    PUBLISH_GROUP_ID = 'de.wackernagel.android'
    PUBLISH_ARTIFACT_ID = 'sidekick-ui'
    PUBLISH_VERSION = '1.0.0'

    ANDROID_SUPPORT_VERSION = '24.2.1'
    SDK_VERSION = 24
}

// END BINTRAY RELEASE INFOS

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion SDK_VERSION
        versionCode 1
        versionName PUBLISH_VERSION
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        htmlReport true
        htmlOutput file("build/outputs/lint-results.html")
        abortOnError true
        warningsAsErrors false
        disable 'IconMissingDensityFolder'
    }
}

dependencies {
    compile "com.android.support:support-v4:$ANDROID_SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$ANDROID_SUPPORT_VERSION"
    compile "com.android.support:preference-v7:$ANDROID_SUPPORT_VERSION"
}

// START BINTRAY RELEASE TASK

apply from: "android-release-aar.gradle"

task bintray( dependsOn: [ "clean", "build", "generateRelease" ] ) {
    build.mustRunAfter( clean )
    generateRelease.mustRunAfter( build )

    doLast {
        copy {
            from "${buildDir}"
            into "${projectDir}/bintray"
            include '*.zip'
        }
        println "Bintray release created"
    }
}

// END BINTRAY RELEASE TASK

// START JACOCO CODE COVERAGE TASK

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.9"
}

def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*'
]
def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
def mainSrc = "${project.projectDir}/src/main/java"

task jacoco(type: JacocoReport, dependsOn: 'test') {

    reports {
        html.enabled = true
        html.destination = "${buildDir}/reports/jacoco"
    }

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")

    doLast {
        println "Report created at ${buildDir}/reports/jacoco/index.html"
    }
}
